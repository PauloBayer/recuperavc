plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {
    namespace 'com.recuperavc'
    compileSdk 34

    buildFeatures { compose true }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14"
    }

    defaultConfig {
        minSdk 26
        targetSdk 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }

        externalNativeBuild {
            cmake {
                // When set, builds whisper.android against the version located
                // at GGML_HOME instead of the copy bundled with whisper.cpp.
                if (
                        project.hasProperty('GGML_HOME') &&
                                project.findProperty('GGML_CLBLAST') == 'ON'
                ) {
                    // Turning on CLBlast requires GGML_HOME
                    arguments "-DGGML_HOME=${project.property('GGML_HOME')}",
                            "-DGGML_CLBLAST=ON",
                            "-DOPENCL_LIB=${project.property('OPENCL_LIB')}",
                            "-DCLBLAST_HOME=${project.property('CLBLAST_HOME')}",
                            "-DOPENCL_ROOT=${project.property('OPENCL_ROOT')}",
                            "-DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE=BOTH",
                            "-DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY=BOTH"
                } else if (project.hasProperty('GGML_HOME')) {
                    arguments "-DGGML_HOME=${project.property('GGML_HOME')}"
                }
            }
        }
    }

    buildTypes {
        release { minifyEnabled false }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions { jvmTarget = '17' }

    packagingOptions {
        resources { excludes += '/META-INF/{AL2.0,LGPL2.1}' }
    }

    ndkVersion "25.2.9519653"

    externalNativeBuild {
        cmake {
            path = file("src/main/jni/whisper/CMakeLists.txt")
        }
    }
}

dependencies {

    implementation(platform("androidx.compose:compose-bom:2024.06.00"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.foundation:foundation")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.animation:animation")

    debugImplementation("androidx.compose.ui:ui-tooling")
    debugImplementation("androidx.compose.ui:ui-test-manifest")

    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1"

    def roomVersion = "2.6.1"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt         "androidx.room:room-compiler:$roomVersion"

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

kapt {
    correctErrorTypes = true
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas")
        arg("room.incremental", "true")
        arg("room.expandProjection", "true")
    }
}
